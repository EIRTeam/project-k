#!/usr/bin/env python

Import("env")
Import("env_modules")

env_worldgen = env_modules.Clone()

# Thirdparty source files

thirdparty_obj = []

thirdparty_dir = "thirdparty/"
thirdparty_sources = [
    "sdnoise.c",
]
thirdparty_sources = [thirdparty_dir + file for file in thirdparty_sources]

env_worldgen.Append(CPPDEFINES=["TF_DISABLE_EXCEPTION_HANDLING"])

env_thirdparty = env_worldgen.Clone()
env_thirdparty.Prepend(CPPPATH=[thirdparty_dir])
env_thirdparty.disable_warnings()
env_thirdparty.add_source_files(thirdparty_obj, thirdparty_sources)
env.modules_sources += thirdparty_obj

# Godot source files

module_obj = []

env_worldgen.add_source_files(module_obj, "*.cpp")
env_worldgen.add_source_files(module_obj, "vehicle/*.cpp")
env.modules_sources += module_obj

# Needed to force rebuilding the module files when the thirdparty library is updated.
env.Depends(module_obj, thirdparty_obj)
